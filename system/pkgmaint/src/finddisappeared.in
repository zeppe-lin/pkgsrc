#!/usr/bin/env perl
use strict;
use warnings;
use Getopt::Long qw(:config gnu_compat);

sub finddisappeared {
    my $root = shift || '';

    open my $fh, "< $root/var/lib/pkg/db"
      or die "Couldn't open package database ($root): $!\n";

    local $/ = ""; # read files paragraph-wise
    while (<$fh>) {
        my ($name, $version, @footprint) = split /\n/;

        my @missing = grep ! -e "$root/$_", @footprint;
        next if not @missing;

        print map "$name: /$_\n", @missing;
    }

    close($fh);
}

sub print_help {
    print <<EOF;
Usage: finddisappeared [OPTION]
Find files that are owned by package manager but somehow disappeared.

  -r, --root PATH   specify alternative root location
  -v, --version     print version and exit
  -h, --help        print help and exit
EOF
    exit 0;
}

sub print_version {
    print "finddisappeared (pkgmaint) \@VERSION@\n";
    exit 0;
}

sub main {
    GetOptions(
      "r|root=s"  => \my $opt_root,
      "v|version" => \my $opt_version,
      "h|help"    => \my $opt_help,
    ) or die "Error in command-line arguments\n";

    &print_help    if $opt_help;
    &print_version if $opt_version;

    die "finddisappeared: invalid option @ARGV\n" if @ARGV;

    finddisappeared $opt_root;
}

main() if not caller();
1;

# vim:sw=4:ts=4:sts=4:et:cc=72:tw=70
# End of file.
